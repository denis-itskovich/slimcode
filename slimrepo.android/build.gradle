buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.3'
        classpath 'com.jakewharton.sdkmanager:gradle-plugin:0.12.0'
        classpath 'org.robolectric:robolectric-gradle-plugin:1.0.1'
    }
}

apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.library'
apply plugin: 'org.robolectric'
apply plugin: 'maven-publish'
apply plugin: 'idea'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 22
        versionCode 1
        versionName version
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

publishing {
    repositories {
        mavenLocal()
    }

    publications {
        maven(MavenPublication) {
            artifactId 'slimrepo-android'
            artifact '${project.buildDir}/outputs/aar/${project.name}-release.aar'
            artifact (sourceJar)
        }
    }
}

robolectric {
    afterTest { descriptor, result ->
        println "TEST ${result.resultType} - ${descriptor.name}"
    }
}

if (hasProperty('bintrayUser') && hasProperty('bintrayKey')) {
    bintray {
        user = bintrayUser
        key = bintrayKey
        publications = ['maven'] //When uploading Maven-based publication files
        dryRun = false //Whether to run this as dry-run, without deploying
        publish = true //If version should be auto published after an upload
        pkg {
            repo = bintrayRepo
            name = 'slimrepo-android'
            userOrg = bintrayOrg //An optional organization name when the repo belongs to one of the user's orgs
            desc = 'Slim Repository Android Core Library'
            websiteUrl = bintrayWebsiteUrl
            issueTrackerUrl = bintrayIssueTrackerUrl
            vcsUrl = bintrayVcsUrl
            licenses = ['Apache-2.0']
            labels = ['android','java','orm','sqlite']
            publicDownloadNumbers = bintrayPublicDownloadNumbers
            version {
                name = project.version //Bintray logical version name
            }
        }
    }
}


dependencies {
    compile project(':slimrepo.core')

    testCompile project(path: ':slimrepo.core', configuration: 'testOutput')
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.0.5-beta'
    testCompile ('org.robolectric:robolectric:3.0-rc2') {
        exclude group: 'commons-logging', module: 'commons-logging'
    }
}
