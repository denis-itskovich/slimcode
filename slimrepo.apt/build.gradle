import org.gradle.internal.jvm.Jvm

apply plugin: 'java'
apply plugin: 'maven-publish'

group = 'com.slimgears.slimorm'
version = '0.2'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources"
    into "${buildDir}/classes/test"
}

task copyResources(type: Copy) {
    from "${projectDir}/src/main/resources"
    into "${buildDir}/classes/main"
}

processTestResources.dependsOn copyTestResources
processResources.dependsOn copyResources

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'slimorm-apt'
            from components.java
        }
    }
}

publishing {
    repositories {
        if (hasProperty('localMavenRepoPath')) {
            maven {
                url localMavenRepoPath
            }
        }
    }
}

test {
    afterTest { descriptor, result ->
        println "TEST ${result.resultType} - ${descriptor.name}"
    }
}

dependencies {
    compile 'com.squareup:javapoet:1.0.0'
    compile 'com.google.guava:guava:18.0'
    compile project(':slimrepo.core')

    testCompile 'junit:junit:4.12'
    testCompile 'com.google.testing.compile:compile-testing:0.6'
    testCompile 'com.google.truth:truth:0.25'
    testCompile 'org.mockito:mockito-core:2.0.5-beta'

    testCompile files(Jvm.current().getToolsJar())
}
